plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id "org.owasp.dependencycheck" version "8.1.0"
    id "com.github.ben-manes.versions" version "0.45.0"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'progbits'
        url = "https://archiva.progbits.com/coffer/repository/internal"
    }
}

ext {
	jettyVersion = '11.0.14'
	log4jVersion = '2.19.0'
	jacksonDataBind = '2.14.2'
	jacksonDataFormat = '2.14.2'
}

dependencies {
    // Use TestNG framework, also requires calling test.useTestNG() below
    testImplementation 'org.testng:testng:7.7.0'

    api("org.eclipse.jetty:jetty-server:$jettyVersion") {
		exclude group: 'org.slf4j', module: 'slf4j-api'
	}

	api("org.eclipse.jetty:jetty-servlet:$jettyVersion") {
		exclude group: 'org.slf4j', module: 'slf4j-api'
	}

	api("org.eclipse.jetty.websocket:websocket-jetty-server:$jettyVersion") {
		exclude group: 'org.slf4j', module: 'slf4j-api'
	}

	api("org.apache.logging.log4j:log4j-api:$log4jVersion")
	api("org.apache.logging.log4j:log4j-core:$log4jVersion")
	api("org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion")
	api("org.apache.logging.log4j:log4j-layout-template-json:$log4jVersion")
	api("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonDataFormat")
	api("com.fasterxml.jackson.core:jackson-databind:$jacksonDataBind")
	api("org.slf4j:slf4j-api:1.7.36")
	
}

tasks.named('test') {
    // Use TestNG for unit tests.
    useTestNG()
}

task getDeps(type: Copy) {
  from sourceSets.main.runtimeClasspath
  into 'lib/'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "$artifactId"
            from components.java 
           versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
		}
	}

	repositories {
        maven {
            name = 'progbits'
            url = "https://archiva.progbits.com/coffer/repository/internal"
            credentials(PasswordCredentials)
        }
    }
}
